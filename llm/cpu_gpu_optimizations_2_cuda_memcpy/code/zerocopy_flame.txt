#include <cstdio>
#include <cstdlib>
#include <cuda_runtime.h>

#define CHECK_CUDA(call)                                      \
do {                                                           \
    cudaError_t err = call;                                   \
    if (err != cudaSuccess) {                                  \
        fprintf(stderr, "CUDA error %s:%d: %s\n",              \
                __FILE__, __LINE__, cudaGetErrorString(err));  \
        exit(EXIT_FAILURE);                                    \
    }                                                           \
} while (0)

__global__ void editData(float* data, int N) {
    int idx = blockIdx.x * blockDim.x + threadIdx.x;
    if (idx < N)
        data[idx] += 2.0f;
}

int main() {
    const int N = 50'000'000;   // large enough to trigger noticeable pin cost
    const int iterations = 50;  // repeat to amplify effect

    int threadsPerBlock = 256;
    int blocksPerGrid = (N + threadsPerBlock - 1) / threadsPerBlock;
    size_t transfer_size = N * sizeof(float);

    for (int it = 0; it < iterations; it++) {

        float *host_mem = nullptr;
        float *device_ptr = nullptr;

        // Allocate mapped pinned memory
        CHECK_CUDA(cudaHostAlloc(
            (void**)&host_mem,
            transfer_size,
            cudaHostAllocMapped
        ));

        // Initialize memory (touch pages)
        for (int i = 0; i < N; i++)
            host_mem[i] = 1.0f;

        // Get device pointer (zero-copy)
        CHECK_CUDA(cudaHostGetDevicePointer(
            (void**)&device_ptr,
            host_mem,
            0
        ));

        // Launch kernel that accesses host memory via PCIe
        editData<<<blocksPerGrid, threadsPerBlock>>>(device_ptr, N);
        CHECK_CUDA(cudaGetLastError());

        CHECK_CUDA(cudaDeviceSynchronize());

        // Free pinned memory (triggers unpin)
        CHECK_CUDA(cudaFreeHost(host_mem));
    }

    printf("Done.\n");
    return 0;
}

/*
# How to run this CUDA script

# Step 1 - Install Dependencies for perf
sudo apt update
sudo apt install -y linux-tools-common linux-tools-generic linux-tools-$(uname -r)
sudo apt install -y linux-headers-$(uname -r) git perl xdg-utils

# Step 2 - Get the FlameGraph toolkit
#        - a new folder called FlameGraph is cloned
git clone https://github.com/brendangregg/FlameGraph.git

# only done once:
echo 'export PATH=$PATH:<your path to>/FlameGraph' >> ~/.bashrc
source ~/.bashrc

# Step 3 - Compile the CUDA code
nvcc -O2 zerocopy_flame.cu -o zerocopy_flame

# Step 4 - Record CPU Samples
sudo perf record -F 999 -g ./zerocopy_flame

# Step 5 - Generate the Flame Graph
sudo perf script | ./FlameGraph/stackcollapse-perf.pl | ./FlameGraph/flamegraph.pl > zerocopy_flamegraph.svg

# Step 6 Optional - check if pinning symbols are in the svg
grep -iE "pin_user_page" zerocopy_flamegraph.svg
*/
